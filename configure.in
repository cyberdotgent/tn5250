dnl ** Process this file with autoconf to produce a configure script.
AC_INIT(src/tn5250.c)

dnl * Checks for programs.
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AM_PROG_LIBTOOL

dnl ** Automake Intialization
AM_INIT_AUTOMAKE(tn5250, 0.17.1)
AM_CONFIG_HEADER(src/tn5250-config.h)
AC_ARG_ENABLE(old-keys,
[  --enable-old-keys       Use the old key handler, which has some quirks
                          but might be more complete.],,[
  AC_DEFINE_UNQUOTED(USE_OWN_KEY_PARSING,1)
])

dnl *
dnl * Not using gnome, so look for glib and GTK+ instead.
dnl *
AM_PATH_GLIB

dnl **************************************************************************
dnl * Python interface support.						     *
dnl **************************************************************************

AC_PATH_PROG(PYTHON,python,no)
if test x$PYTHON != xno ; then
  AC_MSG_CHECKING(for python prefix)
  PYTHON_PREFIX="`$PYTHON -c 'import sys; print sys.exec_prefix' 2>&5`"
  AC_SUBST(PYTHON_PREFIX)
  AC_MSG_RESULT($PYTHON_PREFIX)

  AC_MSG_CHECKING(for python version)
  PYTHON_VERSION="`$PYTHON -c 'import sys; print sys.version[[:3]]' 2>&5`"
  AC_SUBST(PYTHON_VERSION)
  AC_MSG_RESULT($PYTHON_VERSION)
fi

dnl *
dnl * Enable the python interface if we have everything we need to enable it
dnl * or if the user has explicitly told us to.
dnl *
AC_ARG_ENABLE(python,
[  --enable-python         Enable python language support.],
[ if test x$enable_python = xyes ; then
    if test x$PYTHON = xno ; then
      AC_MSG_ERROR(** --enable-python specified and python binary not found **)
    fi
  fi
  PYTHON_CFLAGS="-I$PYTHON_PREFIX/include/python$PYTHON_VERSION"
],[
  enable_python=no
  PYTHON_CFLAGS=" "
])
AC_SUBST(PYTHON_CFLAGS)
AC_MSG_CHECKING(for whether to enable python support)
AM_CONDITIONAL(PYTHON, test x$enable_python = xyes)
AC_MSG_RESULT($enable_python)

AC_PATH_PROG(DIALOG,dialog,no)
if test x$DIALOG = xno ; then
	AC_PATH_PROG(DIALOG,whiptail,no)
fi
AC_SUBST(DIALOG)

dnl **
dnl ** Check for screen interface libraries.  There are a few posibilities here:
dnl ** -lncurses, -lcurses, -lslang.  -lslang is only checked for if the 
dnl ** --with-slang option is passed to ./configure
dnl **
dnl ** Under (at least) some BSDs, both curses and ncurses is installed.  In that
dnl ** case, we prefer ncurses, and must use <ncurses.h>
dnl **
AC_ARG_WITH(slang,[  --with-slang            use the S/Lang library for screen management.],[
if test "$with_slang" != "no"
then
	USE_SLANG=1
	AC_DEFINE_UNQUOTED(USE_SLANG,1)
else
	USE_CURSES=1
	AC_DEFINE_UNQUOTED(USE_CURSES,1)
fi
],[
USE_CURSES=1
AC_DEFINE_UNQUOTED(USE_CURSES,1)
])
if test "$USE_CURSES" = "1"
then
	AC_CHECK_LIB(ncurses, initscr)
	if test "$ac_cv_lib_ncurses_initscr" != "yes"
	then
		dnl Check for -lcurses if -lncurses isn't found.
		AC_CHECK_LIB(curses, initscr)
		if test "$ac_cv_lib_curses_initscr" != "yes"
		then
			AC_MSG_ERROR([** You need a curses-compatible library installed.])
		fi
	fi
	AC_CHECK_HEADERS(ncurses.h curses.h,break)
	if test "$ac_cv_header_ncurses_h" != yes -a "$ac_cv_header_curses_h" != yes
	then
		AC_MSG_ERROR([** Can't find ncurses.h or curses.h **])
	fi
	AC_CHECK_HEADERS(termcap.h)
	
	#
	# Check to see if the curses implementation has attr_t defined -- some older
	# versions of GNU curses don't have it.
	#
	AC_MSG_CHECKING(for attr_t in curses)
	HAVE_ATTR_T=no
	AC_TRY_COMPILE([
#include <stdlib.h>
#include <stdio.h>
#ifdef HAVE_NCURSES_H
#include <ncurses.h>
#else
#include <curses.h>
#endif
],[ attr_t testvar; ],[HAVE_ATTR_T=yes])
	if test x$HAVE_ATTR_T = xno ; then
		AC_DEFINE_UNQUOTED(attr_t,int)
	fi
	AC_MSG_RESULT($HAVE_ATTR_T)
else
	AC_CHECK_LIB(slang, SLang_init_tty)
	if test "$ac_cv_lib_slang_SLang_init_tty" != "yes"
	then
		AC_MSG_ERROR([** Can't find -lslang **])
	fi
fi

dnl ** Check for libraries
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(nsl, main)

dnl **
dnl **  Check for OpenSSL library & associated crypto library
dnl **
AC_ARG_WITH(ssl,[  --with-ssl              compile support for OpenSSL encryption into emulator],[
if test "$with_ssl" != "no"
then
     AC_CHECK_LIB(crypto,CRYPTO_num_locks)
     if test "$ac_cv_lib_crypto_CRYPTO_num_locks" != "yes"
     then
             AC_MSG_ERROR([** Unable to find OpenSSL libraries!])
     fi
     AC_CHECK_LIB(ssl,SSL_library_init)
     if test "$ac_cv_lib_ssl_SSL_library_init" != "yes"
     then
             AC_MSG_ERROR([** Unable to find OpenSSL libraries!])
     fi
fi
],[
     AC_CHECK_LIB(crypto,CRYPTO_num_locks)
     if test "$ac_cv_lib_crypto_CRYPTO_num_locks" = "yes"
     then
          AC_CHECK_LIB(ssl,SSL_library_init)
     fi
])

dnl **
dnl ** --with-extra-libs: a must for debugging.
dnl **
AC_ARG_WITH(extra-libs,[  --with-extra-libs=libs  List of extra libraries to link against.  This is
                          good for linking against a malloc debugger, for
			  example.],[
if test x$with_extra_libs = no
then
	with_extra_libs=""
fi
],[
with_extra_libs=""
])

AC_SUBST(with_extra_libs)

AC_PATH_XTRA

dnl ** Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/ioctl.h sys/filio.h sys/time.h unistd.h slang/slang.h slang.h locale.h)
AC_CHECK_HEADERS(netinet/in.h sys/wait.h fcntl.h syslog.h pwd.h)

AC_TYPE_SIGNAL

dnl ** Checks for library functions.
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(setlocale select socket strerror resizeterm getopt_long)
AM_CONDITIONAL(LIB_GETOPT, test x$ac_cv_func_getopt_long = xno)

AC_CHECK_SIZEOF(short, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long, 4)

dnl * True for anything other than Windoze.
AC_DEFINE_UNQUOTED(SOCKET_TYPE,int)

dnl * Decide whether to use OS-specific directories.
AC_CANONICAL_HOST
AC_ARG_ENABLE(os-dir,
	[--enable-os-dir=DIR    Also install the OS-specific files in DIR.  If
                         DIR is not given, determine it automatically.  If
                         DIR is 'no', don't install OS-specific files.],
	[], [enable_os_dir=yes])

AC_MSG_CHECKING(for OS specific directory)
SUBDIRS_ADD=
case "$enable_os_dir" in
	freebsd) SUBDIRS_ADD=freebsd ;;
	linux) SUBDIRS_ADD=linux ;;
	yes)
		case "$host_os" in
			freebsd*) SUBDIRS_ADD=freebsd ;;
			linux*) SUBDIRS_ADD=linux ;;
		esac
	;;
esac
AC_MSG_RESULT(${SUBDIRS_ADD:-none})
AC_SUBST(SUBDIRS_ADD)

AC_OUTPUT(Makefile src/Makefile doc/Makefile linux/Makefile freebsd/Makefile sun/Makefile tn5250-config xt5250)
